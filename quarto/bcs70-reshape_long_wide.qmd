---
layout: default
title: "Reshaping Data from Long to Wide (or Wide to Long)"
nav_order: 4
parent: BCS70
format: 
  gfm:
    variant: +yaml_metadata_block
jupyter: stata
---

- [Download the Stata script for this page](../do_files/bcs70-reshape_long_wide.do)

# Introduction

In this section, we show how to reshape data from long to wide (and vice versa). To demonstrate this, we use data  on cohort member's height and weight from Sweep 9 (42y) and Sweep 11 (51y).

# Reshaping Raw Data from Wide to Long

We begin by loading the data from each sweep and merging these together into a single *wide* format data frame; see [Combining Data Across Sweeps](https://cls-data.github.io/docs/bcs70-merging_across_sweeps.html) for further explanation on how this is achieved. Note, the names of the height and weight variables in Sweep 8 and Sweep 11 follow a similar convention, which is the exception rather than the rule in BCS70 data (at least in early sweeps). Below, we convert the variable names in the Sweep 9 dataset to lower case so that they closely match those in the Sweep 11 dataset. This will make reshaping easier.

```{stata}
* Load 42y data
use BCSID BD9HGHTM BD9WGHTK using "42y/bcs70_2012_derived.dta", clear
rename *, lower
save "temp_42y.dta", replace

* Load 51y data
use bcsid bd11hghtm bd11wghtk using "51y/bcs11_age51_main.dta", clear
save "temp_51y.dta", replace

* Merge
use "temp_42y.dta", clear
merge 1:1 bcsid using "temp_51y.dta"
keep if _merge == 3 // Keeps matches. `merge ..., keep(3)` would also work.
drop _merge
```

We can reshape the dataset into wide format using `reshape long`. This requires variables to be named in a specific format: `stub` + `suffix`. Here, we want our stubs to be `hghtm` and `wghtk`, and our suffixes to be the sweep numbers (9 and 11). We can do this using `rename` with the `*` wildcard operator. Then in the `reshape long` command, we specify the stubs in the main command, with the identifier and suffix variables specified in the `i()` and `j()` command options respectively. Note the identifier (`i()`) exists in the data, while the suffix variable (`j()`) is created during the reshape process.

```{stata}
* Rename
rename bd9* *9
rename bd11* *11

* Reshape Long
reshape long hghtm wghtk, i(bcsid) j(sweep)
list * in 1/10
```

# Reshaping Raw Data from Long to Wide

We can reshape the data from long to wide format using the `reshape wide` command. We again specify the stubs in the main command, and the identifier and suffix variables in `i()` and `j()`, respectively. The suffix variable exists in the data, but the value of this are used to create new variable names during the reshape process.

```{stata}
reshape wide hghtm wghtk, i(bcsid) j(sweep)
list * in 1/10
```
