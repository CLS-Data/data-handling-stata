---
layout: default
title: "Reshaping Data from Long to Wide (or Wide to Long)"
nav_order: 4
parent: NCDS
format: 
  gfm:
    variant: +yaml_metadata_block
jupyter: stata
---

- [Download the Stata script for this page](../do_files/ncds-reshape_long_wide.do)

# Introduction

In this section, we show how to reshape data from long to wide (and vice versa). To demonstrate, we use data from Sweeps 4 (23y) and 8 (50y) on cohort member's height and weight collected.

The commands we use are:

```{stata}
clear all
set more off
```

# Reshaping Raw Data from Wide to Long

We begin by loading the data from each sweep and merging these together into a single *wide* format data frame; see [Combining Data Across Sweeps](https://cls-data.github.io/docs/ncds-merging_across_sweeps.html) for further explanation on how this is achieved. Note, the names of the height and weight variables in Sweep 4 and Sweep 8 follow a similar convention, which is the exception rather than the rule in NCDS data. Below, we convert the variable names in the Sweep 4 data frame to upper case so that they closely match those in the Sweep 8 dataset. This will make reshaping easier.

```{stata}
* Load 23y data
use ncdsid dvwt23 dvht23 using "23y/ncds4.dta", clear
rename *, upper
save "temp_23y.dta", replace

* Load 50y data
use NCDSID DVWT50 DVHT50 using "50y/ncds_2008_followup.dta", clear
save "temp_50y.dta", replace

* Merge
use "temp_23y.dta", clear
merge 1:1 NCDSID using "temp_50y.dta"
keep if _merge == 3 // Keeps matches. `merge ..., keep(3)` would also work.
drop _merge
```

We can reshape the dataset into wide format using `reshape long`. This requires variables to be named in a specific format: `stub` + `suffix`. Here, we want our stubs to be `DVWT` and `DVHT`, and our suffixes to be the sweep numbers (23 and 50). We can do this using `rename` with the `*` wildcard operator. Then in the `reshape long` command, we specify the stubs in the main command, with the identifier and suffix variables specified in the `i()` and `j()` command options respectively. Note the identifier (`i()`) exists in the data, while the suffix variable (`j()`) is created during the reshape process.

```{stata}
reshape long DVHT DVWT, i(NCDSID) j(fup)
list * in 1/10
```

# Reshaping Raw Data from Long to Wide

We can reshape the data from long to wide format using the `reshape wide` command. We again specify the stubs in the main command, and the identifier and suffix variables in `i()` and `j()`, respectively. The suffix variable exists in the data, but the value of this are used to create new variable names during the reshape process.

```{stata}
reshape wide DVHT DVWT, i(NCDSID) j(fup)
list * in 1/10
```
